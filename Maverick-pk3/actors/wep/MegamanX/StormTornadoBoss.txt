Actor StormTornadoBoss : BaseMM8BDMWepX
{
	Tag "StormEagle"
	//Obituary
	Weapon.AmmoType "StormTornadoAmmo"
	Weapon.AmmoType2 "StormFeathersAmmo"
	Inventory.PickupMessage "Death from above."
	States
	{
		Spawn:
		C_X1 B 1
		loop
		
		Ready:
		CUTA A 0
		CUTA A 0 A_GunFlash
		CUTA A 0 A_GiveInventory("IsNotAltfiring",1)
		goto Ready1
		
		Ready1:
		CUTA A 12 A_WeaponReady
		CUTA A 0 A_GiveInventory("StormTornadoAmmo",6)
		CUTA A 0 A_GiveInventory("StormFeathersAmmo",2)
		CUTA A 3
		loop
		
		Ready2:
		CUTA A 0 A_GiveInventory("IsNotAltfiring",1)
		goto Ready1
		
		Deselect:
		TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		DRIA A 1 A_Lower
		Loop
		
		Select:
		TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		DRIA A 1 A_Raise
		Loop
		
		Fire:
		CUTA A 0 A_JumpIfInventory("IsNotAltfiring",1,1)
		goto Ready1
		CUTA A 0 A_JumpIfInventory("EagleFlagF",1,"EggAttackCheck")
		CUTA A 1 A_JumpIfInventory("StormTornadoAmmo",28,"ChargerLoop")
		CUTA A 4
		goto MainEnd
		ChargerLoop:
		CUTA A 4
		CUTA A 1 A_Refire("Charge1")
		goto MainEnd
		Charge1:
		CUTA A 4
		CUTA A 1 A_Refire("Charge2")
		goto MainEnd
		Charge2:
		CUTA A 4
		CUTA A 1 A_Refire("Charge3")
		goto MainEnd
		Charge3:
		CUTA A 4
		CUTA A 1 A_Refire("Charge4")
		goto MainEnd
		Charge4:
		CUTA A 4
		CUTA A 1 A_Refire("Charge5")
		goto MainEnd
		Charge5:
		CUTA A 4
		CUTA A 1 A_Refire("Charge6")
		goto MainEnd
		Charge6:
		CUTA B 4
		CUTA B 0 A_GiveInventory("EagleChargedFlag",1)
		CUTA B 1 A_Refire("Charge6")
		goto MainEnd
		MainEnd:
		CUTA A 0 A_GiveInventory("StormEagleMain1_CI",1)
		CUTA A 11
		goto Ready1
		
		FireFeathersCheck:
		CUTA A 0 A_JumpIfInventory("StormFeathersAmmo",14,"FireFeathers")
		goto Ready1
		FireFeathers:
		CUTA A 0 A_GiveInventory("StormEagleMain3_CI",1)
		CUTA A 0 A_TakeInventory("EagleFeatherFlag",1)
		CUTA A 0 A_TakeInventory("StormFeathersAmmo",14)
		CUTA A 4
		CUTA A 0 A_GiveInventory("EagleFeatherDelay",28)
		Goto Ready1
		
		EggAttackCheck:
		CUTA A 1 A_JumpIfInventory("StormTornadoAmmo",24,"EggAttack")
		Goto Ready1
		EggAttack:
		TNT1 A 0 A_PlaySoundEx("weapon/FIRE1","Weapon")
		CUTA A 0 A_GiveInventory("StormEagleMain2_CI",1)
		CUTA A 0 A_TakeInventory("StormTornadoAmmo",28)
		CUTA A 10
		Goto Ready1
		
		Altfire:
		CUTA A 0 A_JumpIfInventory("EagleFlagF",1,"FlightDiveCheck")
		CUTA A 0 A_TakeInventory("IsNotAltfiring",1)
		CUTA A 1 A_GiveInventory("EagleFeatherFlag",1)
		TNT1 A 0 A_PlaySoundEx("weapon/SEFLAP","Weapon",1)
		goto WingBlowF
		WingBlow:
		CUTA A 0 A_JumpIfInventory("EagleFeatherDelay",1,1)
		goto WingBlowF
		CUTA A 0 A_FireCustomMissile("WingBlower",0,0,0,0)
		CUTA A 2
		CUTA A 0 A_Refire("WingBlow")
		CUTA A 2 A_TakeInventory("EagleFeatherFlag",1)
		CUTA A 0 A_StopSound(1)
		Goto Ready2
		WingBlowF:
		CUTA A 0 A_JumpIfInventory("EagleFeatherDelay",1,"WingBlow")
		CUTA A 0 A_FireCustomMissile("WingBlower",0,0,0,0)
		CUTA A 1 A_JumpIf(ACS_NamedExecuteWithResult("Get_Button_M") == 1, "FireFeathersCheck")
		CUTA A 1 A_JumpIf(ACS_NamedExecuteWithResult("Get_Button_M") == 1, "FireFeathersCheck")
		CUTA A 0 A_Refire("WingBlowF")
		CUTA A 2 A_TakeInventory("EagleFeatherFlag",1)
		CUTA A 0 A_StopSound(1)
		Goto Ready2
		
		FlightDiveCheck:
		CUTA A 0 A_JumpIfInventory("StormFeathersAmmo",14,"FlightDiveStart")
		goto Ready1
		FlightDiveStart:
		CUTA A 0 A_TakeInventory("StormFeathersAmmo",14)
		TNT1 A 0 A_GiveInventory("EagleDivePitch",1)
		TNT1 A 0 A_PlaySoundEx("weapon/SEDIVE","Weapon")
		TNT1 A 0 A_SetArg(2,momx)
		TNT1 A 0 A_SetArg(3,momy)
		TNT1 A 0 A_SetArg(4,momz)
		FlightDive:
		TNT1 A 0 A_JumpIf(z-floorz==0,"FlightDiveEnd")
		TNT1 A 0 A_JumpIf(momz==0,"Recovery_Z")
		TNT1 A 0 A_ChangeVelocity(args[2],args[3],args[4]-10,2)
		TNT1 A 0 A_JumpIfInventory("EagleDiveFlag",50,"FlightDiveEnd")
		CUTA A 1 A_GiveInventory("EagleDiveFlag",1)
		CUTA A 2 A_ChangeVelocity(args[2],args[3],args[4]-10,2)
		CUTA A 1
		loop
		Recovery_Z:
		TNT1 A 1
		TNT1 A 0 A_JumpIf(z-floorz==0,"FlightDiveEnd")
		TNT1 A 0 A_JumpIf(momz==0,"FlightDiveEnd")
		Goto FlightDiveStart+3
		FlightDiveEnd:
		CUTA A 0 A_FireCustomMissile("EagleDive",0,0,8,0)
		CUTA A 30 A_Stop
		TNT1 A 0 A_TakeInventory("EagleDiveFlag",999)
		Goto Ready1
		
		Flash:
		TNT1 A 0 A_JumpIfInventory("IsDead",1,"NoFlash")
		TNT1 A 2 A_TakeInventory("EagleFeatherDelay",1)
		TNT1 A 0 A_JumpIfInventory("FlightDisableFlag",1,"NoFlight")
		TNT1 A 0 A_JumpIfInventory("EagleFlagF",1,"Flight")
		goto NoFlight
		Flight:
		TNT1 A 0 A_JumpIf(z-floorz>=160,"NoFlight")
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(107,0)==1,"MaybeFlight")
		YesFlight:
		TNT1 A 1 SetPlayerProperty(0,1,3)
		TNT1 A 0 A_JumpIfInventory("StaminaFlag",500,"Flash")
		TNT1 A 0 A_JumpIf(z-floorz==0,"Recharge")
		goto Flash
		MaybeFlight:
		TNT1 A 0 A_TakeInventory("StaminaFlag",20)
		TNT1 A 0 A_JumpIfInventory("StaminaFlag",1,"YesFlight")
		goto NoFlight
		NoFlight:
		TNT1 A 1 SetPlayerProperty(0,0,3)
		TNT1 A 0 A_JumpIfInventory("StaminaFlag",500,"Flash")
		TNT1 A 0 A_JumpIf(z-floorz==0,"Recharge")
		Goto Flash
		Recharge:
		TNT1 A 0 A_GiveInventory("StaminaFlag",4)
		Goto Flash
	}
}

/////////////////////
/*----Inventory----*/
/////////////////////
Actor EagleFlightItem : CustomInventory
{
	inventory.amount 1
	inventory.maxamount 1
	inventory.pickupmessage "Someone else needs to come up with these pickup messages."
	inventory.icon "SEAGZ"
	scale 2.0
	+INVBAR
	States
	{
		Spawn:
		BHOR Z 1
		loop
		Use:
		TNT1 A 0 A_PlaySoundEx("item/ItemUseMM9","SoundSlot6")
		TNT1 A 0 A_JumpIfInventory("EagleFlagF",1,"Take")
		TNT1 A 0 A_GiveInventory("EagleFlagF",1)
		fail
		Take:
		TNT1 A 0 A_TakeInventory("EagleFlagF",999)
		fail
	}
}

actor EagleDivePitch : CustomInventory
{
	States
	{
		Pickup:
		TNT1 A 0 //A_LogInt(pitch)
		TNT1 A 0 A_JumpIf(pitch<=90 && pitch>=20,"Yes")
		TNT1 A 0 A_ChangeVelocity(cos(20)*50,0,-sin(20)*50,3)
		stop
		Yes:
		TNT1 A 0 A_ChangeVelocity(cos(pitch)*50,0,-sin(pitch)*50,3)
		stop
	}
}

Actor StormTornadoAmmo : Ammo
{
	inventory.amount 1
	inventory.maxamount 84
	+INVENTORY.IGNORESKILL
}

Actor StormFeathersAmmo : Ammo
{
	inventory.amount 1
	inventory.maxamount 28
	+INVENTORY.IGNORESKILL
}

actor EagleDiveFlag : Inventory
{
	inventory.amount 1
	inventory.maxamount 50
}


Actor EagleFeatherDelay : Inventory
{
	inventory.amount 1
	inventory.maxamount 28
}

Actor EagleChargedFlag : OnceC{}
Actor EagleFlagF : OnceC{}
Actor EagleFeatherFlag : OnceC{}
Actor IsNotAltfiring : OnceC{}

///////////////////
/*----Weapons----*/
///////////////////
actor StormTornadoStarter
{
	Translation "199:199=41:41", "202:202=210:210"
	PROJECTILE
	Damagetype "StormTornado"
	//Obituary "$OB_TOMAHAWKFEATHER"
	+RIPPER
	+SKYEXPLODE
	+DONTREFLECT
	+NOTARGETSWITCH
	damage (0)
	Radius 8
	Height 8
	speed 1
	scale 2.5
	States
	{
		Spawn:
		TNT1 A 0 A_SetAngle(angle+180.0)
		TNT1 A 0 A_JumpIfInTargetInventory("EagleChargedFlag",1,"FullChargeSpawner")
		goto NoCharge
		FullChargeSpawner:
		TNT1 A 0 A_TakeFromTarget("StormTornadoAmmo",28)
		TNT1 A 0 A_TakeFromTarget("EagleChargedFlag",1)
		TNT1 A 0 A_PlaySoundEx("weapon/SESTORM","Weapon")
		TORG T 1
		TORG T 1 A_SpawnItemEx("StormTornadoFollower",0,0,0,momx*20,momy*20,momz*20,0,41)
		TORG U 1
		TORG U 1 A_SpawnItemEx("StormTornadoFollower",0,0,0,momx*20,momy*20,momz*20,0,41)
		TORG T 1
		TORG T 1 A_SpawnItemEx("StormTornadoFollower",0,0,0,momx*20,momy*20,momz*20,0,41)
		TORG U 1
		TORG U 1 A_SpawnItemEx("StormTornadoFollower",0,0,0,momx*20,momy*20,momz*20,0,41)
		TORG U 1
		TORG U 1 A_SpawnItemEx("StormTornadoFollower",0,0,0,momx*20,momy*20,momz*20,0,41)
		TNT1 A 0 A_ScaleVelocity(20.0)
		goto TornadoTravel
		NoCharge:
		TNT1 A 0 A_PlaySoundEx("weapon/FIRE1","Weapon")
		TNT1 A 0 A_TakeFromTarget("EagleChargedFlag",1)
		TNT1 A 0 A_ScaleVelocity(20.0)
		goto TornadoTravel
		TornadoTravel:
		TORG R 3
		TORG R 0 ACS_ExecuteAlways(347,0,160,160,18)
		TORG S 3 A_Explode(3,80,0,0,80)
		TORG S 0 ACS_ExecuteAlways(347,0,160,160,18)
		TORG T 3
		TORG T 0 ACS_ExecuteAlways(347,0,160,160,18)
		TORG U 3 A_Explode(3,80,0,0,80)
		TORG U 0 ACS_ExecuteAlways(347,0,160,160,18)
		TNT1 A 0 A_ScaleVelocity(1.05)
		loop
		
		Death:
		TNT1 A 1
		Stop
	}
}
actor StormTornadoStarterB : StormTornadoStarter{}
actor StormTornadoStarterR : StormTornadoStarter{}
actor StormTornadoStarterO : StormTornadoStarter{}
actor StormTornadoStarterP : StormTornadoStarter{}

actor StormTornadoFollower : StormTornadoStarter
{
	Translation "199:199=41:41", "202:202=210:210"
	PROJECTILE
	Damagetype "StormTornado"
	//Obituary "$OB_TOMAHAWKFEATHER"
	+RIPPER
	+SKYEXPLODE
	+DONTREFLECT
	+NOTARGETSWITCH
	damage (0)
	Radius 8
	Height 8
	speed 20
	scale 2.5
	States
	{
		Spawn:
		TNT1 A 0 A_SetAngle(angle+180.0)
		TORG RSTU 2
		goto TornadoTravel
		TornadoTravel:
		TORG R 3
		TORG R 0 ACS_ExecuteAlways(347,0,160,160,18)
		TORG S 3  A_Explode(3,80,0,0,80)
		TORG S 0 ACS_ExecuteAlways(347,0,160,160,18)
		TORG T 3
		TORG T 0 ACS_ExecuteAlways(347,0,160,160,18)
		TORG U 3  A_Explode(3,80,0,0,80)
		TORG U 0 ACS_ExecuteAlways(347,0,160,160,18)
		TNT1 A 0 A_ScaleVelocity(1.05)
		loop
		
		Death:
		TNT1 A 1
		Stop
	}
}

actor StormTornadoFollowerB : StormTornadoFollower{}
actor StormTornadoFollowerR : StormTornadoFollower{}
actor StormTornadoFollowerO : StormTornadoFollower{}
actor StormTornadoFollowerP : StormTornadoFollower{}

actor BirdEgg
{
	Translation "199:199=41:41", "202:202=210:210"
	PROJECTILE
	//Damagetype "StormTornado"
	//Obituary "$OB_TOMAHAWKFEATHER"
	-NOGRAVITY
	+SKYEXPLODE
	Gravity 0.50
	Damage (10)
	Radius 16
	Height 56
	Speed 40
	scale 2.5
	States
	{
		Spawn:
		BURX W 1
		loop
		
		XDeath:
		BURX W 0 A_SpawnItemEX("BabyBird",20,20,0,16,0,0,0,41)
		BURX W 0 A_SpawnItemEX("BabyBird",0,20,20,16,0,0,0,41)
		stop
		
		Death:
		BURX W 0 A_SpawnItemEX("BabyBird",20,20,0,16,0,0,0,41)
		BURX W 0 A_SpawnItemEX("BabyBird",0,20,20,16,0,0,0,41)
		BURX W 0 A_SpawnItemEX("BabyBird",20,0,0,16,0,0,0,41)
		BURX W 0 A_SpawnItemEX("BabyBird",0,0,20,16,0,0,0,41)
		stop
	}
}

actor BirdEggB : BirdEgg{}
actor BirdEggR : BirdEgg{}
actor BirdEggO : BirdEgg{}
actor BirdEggP : BirdEgg{}

actor BabyBird
{
	Translation "195:195=4:4", "204:204=212:212"
	PROJECTILE
	//Damagetype "StormTornado"
	//Obituary "$OB_TOMAHAWKFEATHER"
	+DONTSEEKINVISIBLE
	+SEEKERMISSILE
	Radius 6
	Height 5
	scale 2.0
	Damage(6)
	speed 18
	reactiontime 10
	States
	{
		Spawn:
		MGB8 AAABBBCCCDDD 1 A_SeekerMissile(1,3,3,256,5)
		MGB8 A 0 A_CountDown
		loop
		
		Death:
		TNT1 A 0
		stop
	}
}

actor BabyBirdB : BabyBird{}
actor BabyBirdR : BabyBird{}
actor BabyBirdO : BabyBird{}
actor BabyBirdP : BabyBird{}
	
actor EagleFeather
{
	Translation "199:199=41:41", "202:202=210:210"
	PROJECTILE
	//Damagetype "ClassPainSemi"
	//Obituary "$OB_TOMAHAWKFEATHER"
	Radius 7
	Height 7
	scale 2.5
	damage (5)
	speed 25
	States
	{
		Spawn:
		TOMF A 1
		loop
		
		Death:
		TNT1 A 1
		Stop
	}
}

actor EagleFeatherB : EagleFeather{}
actor EagleFeatherR : EagleFeather{}
actor EagleFeatherO : EagleFeather{}
actor EagleFeatherP : EagleFeather{}

actor WingBlower
{
	PROJECTILE
	+NOINTERACTION
	height 0
	radius 0
	speed 0
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_NamedExecuteAlways("C_JAX_SUCKRECODE",0,500,10,6)
		TNT1 A 3
		stop
	}
}

actor EagleDive : BasicExplosion
{
	Damagetype "ClassPainLess2"
	//Obituary "$OB_YAMATODIVE"
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 1 A_Explode(20,84,0,0,42)
		stop
	}
}

/////////////////////
/*----Weapon CI----*/
/////////////////////
Actor StormEagleMain1_CI : TeamColor_CI
{
	States
	{
		FireX:
		TNT1 A 0 A_FireCustomMissile("StormTornadoStarter",0,1,0,0)
		Goto Done
		
		FireB:
		TNT1 A 0 A_FireCustomMissile("StormTornadoStarterB",0,1,0,0)
		Goto Done
		
		FireR:
		TNT1 A 0 A_FireCustomMissile("StormTornadoStarterR",0,1,0,0)
		Goto Done
		
		FireO:
		TNT1 A 0 A_FireCustomMissile("StormTornadoStarterO",0,1,0,0)
		Goto Done
		
		FireP:
		TNT1 A 0 A_FireCustomMissile("StormTornadoStarterP",0,1,0,0)
		Goto Done
	}
}

Actor StormEagleMain2_CI : TeamColor_CI
{
	States
	{
		FireX:
		TNT1 A 0 A_FireCustomMissile("BirdEgg",0,1,0,0)
		Goto Done
		
		FireB:
		TNT1 A 0 A_FireCustomMissile("BirdEggB",0,1,0,0)
		Goto Done
		
		FireR:
		TNT1 A 0 A_FireCustomMissile("BirdEggR",0,1,0,0)
		Goto Done
		
		FireO:
		TNT1 A 0 A_FireCustomMissile("BirdEggO",0,1,0,0)
		Goto Done
		
		FireP:
		TNT1 A 0 A_FireCustomMissile("BirdEggP",0,1,0,0)
		Goto Done
	}
}

Actor StormEagleMain3_CI : TeamColor_CI
{
	States
	{
		FireX:
		TNT1 A 0 A_FireCustomMissile("EagleFeather",-9,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeather",-6,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeather",-3,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeather",0,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeather",3,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeather",6,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeather",9,1,0,0)
		Goto Done
		
		FireB:
		TNT1 A 0 A_FireCustomMissile("EagleFeatherB",-9,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherB",-6,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherB",-3,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherB",0,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherB",3,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherB",6,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherB",9,1,0,0)
		Goto Done
		
		FireR:
		TNT1 A 0 A_FireCustomMissile("EagleFeatherR",-9,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherR",-6,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherR",-3,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherR",0,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherR",3,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherR",6,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherR",9,1,0,0)
		Goto Done
		
		FireO:
		TNT1 A 0 A_FireCustomMissile("EagleFeatherO",-9,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherO",-6,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherO",-3,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherO",0,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherO",3,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherO",6,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherO",9,1,0,0)
		Goto Done
		
		FireP:
		TNT1 A 0 A_FireCustomMissile("EagleFeatherP",-9,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherP",-6,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherP",-3,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherP",0,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherP",3,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherP",6,1,0,0)
		TNT1 A 0 A_FireCustomMissile("EagleFeatherP",9,1,0,0)
		Goto Done
	}
}
